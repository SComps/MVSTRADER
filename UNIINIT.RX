/* REXX */
/* LICENSE? WHAT LICENSE? THIS IS FOR FUN. */
/* INITIALIZE UNIVERSE DATASET */

/* DO NOT DO THIS ON A RUNNING GAME UNLESS */
/* YOU WANT TO RESTART THE ENTIRE GAME */

UNIDSN="'TRADER.UNIVERSE'"
MAXROWS=350
MAXCOLS=350
SEED_PLANETS=3750
SEED_PORTS=10000
PLANETS_PLACED=0
PORTS_PLACED=0
SOL_SECTOR=5
SOL_NAME='Sol'
NEWBIE_PORTS=10 /* FOUND IN FIRST 10 SECTORS */
WANDERER_PORT=1
WANDERER_NAME='The Wanderer'
BLACKHOLES=0
LOTTERIES=0
OLINES=0
/* Universe Structure */
OFFS=DCL('$DEFINE','SECTOR')
OFFS=DCL('SECKEY',1,20,'CHAR')
OFFS=DCL('SECTID',,8,'CHAR')
OFFS=DCL('ROW',,4,'CHAR')
OFFS=DCL('COL',,4,'CHAR')
OFFS=DCL('KPLANET',,20,'CHAR')
OFFS=DCL('SYSNAME',,30,'CHAR')
OFFS=DCL('KPORT',,20,'CHAR')
OFFS=DCL('OWNER',,6,'CHAR')
OFFS=DCL('BLACKHOLE',,1,'CHAR')
OFFS=DCL('LOTTERY',,1,'CHAR')
SAY 'SECTOR RECORD IS 'OFFS' BYTES'

OFFS=DCL('$DEFINE','PORT')
OFFS=DCL('PORKEY',1,20,'CHAR')
OFFS=DCL('TPORT',,6,'CHAR')
OFFS=DCL('TNAME',,30,'CHAR')
OFFS=DCL('TROW',,4,'CHAR')
OFFS=DCL('TCOL',,4,'CHAR')
OFFS=DCL('TOWNER',,8,'CHAR')
OFFS=DCL('TFOOD',,18,'CHAR')
OFFS=DCL('TFPRICE',,18,'CHAR')
OFFS=DCL('TEQUIP',,18,'CHAR')
OFFS=DCL('TEPRICE',,18,'CHAR')
OFFS=DCL('TMATL',,18,'CHAR')
OFFS=DCL('TMPRICE',,18,'CHAR')
OFFS=DCL('TBANK',,18,'CHAR')
SAY 'PORT RECORD IS 'OFFS' BYTES'

OFFS=DCL('$DEFINE','PLANET')
OFFS=DCL('PLAKEY',1,20,'CHAR')
OFFS=DCL('PLANET',,5,'CHAR')
OFFS=DCL('PNAME',,30,'CHAR')
OFFS=DCL('PSECT',,8,'CHAR')
OFFS=DCL('PROW',,4,'CHAR')
OFFS=DCL('PCOL',,4,'CHAR')
OFFS=DCL('POWNER',,8,'CHAR')
OFFS=DCL('PGENDEV',,3,'CHAR')
OFFS=DCL('PFIGHTERS',,18,'CHAR')
OFFS=DCL('PMINES',,18,'CHAR')
OFFS=DCL('PPOSTURE',,1,'CHAR')
OFFS=DCL('PFOOD',,18,'CHAR')
OFFS=DCL('PEQUIP',,18,'CHAR')
OFFS=DCL('PMATL',,18,'CHAR')
OFFS=DCL('PMONEY',,20,'CHAR')
SAY 'PLANET RECORD IS 'OFFS' BYTES'

OFFS=DCL('$DEFINE','DEFENSE')
OFFS=DCL('DEFKEY',1,20,'CHAR')
OFFS=DCL('ROW',,4,'CHAR')
OFFS=DCL('COL',,4,'CHAR')
OFFS=DCL('OWNER',,8,'CHAR')
OFFS=DCL('POSTURE',,1,'CHAR')
OFFS=DCL('FIGHTERS',,15,'CHAR')
OFFS=DCL('MINES',,15,'CHAR')
OFFS=DCL('NUKE',,1,'CHAR')
OFFS=DCL('NUKE_FOR',,6,'CHAR')
OFFS=DCL('BOOT',,1,'CHAR')
SAY 'DEFENSE RECORD IS 'OFFS' BYTES'

OFFS=DCL('$DEFINE','PLAYER')
OFFS=DCL('PLRKEY',1,20,'CHAR')
OFFS=DCL('USERID',,8,'CHAR')
OFFS=DCL('TEAM',,4,'CHAR')
OFFS=DCL('SCORE',,8,'CHAR')
OFFS=DCL('HANDLE',,30,'CHAR')
OFFS=DCL('SHIP',,3,'CHAR')
OFFS=DCL('SHIP_NAME',,30,'CHAR')
OFFS=DCL('HOLDS',,9,'CHAR')
OFFS=DCL('FOOD',,9,'CHAR')
OFFS=DCL('EQUIP',,9,'CHAR')
OFFS=DCL('MATL',,9,'CHAR')
OFFS=DCL('MONEY',,15,'CHAR')
OFFS=DCL('FIGHTERS',,10,'CHAR')
OFFS=DCL('MINES',,10,'CHAR')
OFFS=DCL('GENDEV',,3,'CHAR')
OFFS=DCL('NUKES',,3,'CHAR')
OFFS=DCL('BOOTS',,3,'CHAR')
OFFS=DCL('MODERATED',,1,'CHAR')
OFFS=DCL('SUSPEND',,1,'CHAR')
OFFS=DCL('CURSEC',,8,'CHAR')
SAY 'PLAYER RECORD IS 'OFFS' BYTES'

SAY ' '
SAY 'Do you really, really want to initiate the'
SAY 'BIGBANG sequence? (y/N)'
PULL INITGO
IF STRIP(INITGO)<>'Y' THEN DO
   SAY 'Phew! That could have been bad.'
   EXIT 0
   END
/* IF WE GOT HERE, SOMEBODY IS BRAVE */
ADDRESS TSO
CALL OUTTRAP('TRAP.')
RC=SUBMIT("'TRADER.JCLLIB(UNIVCR)'")
CALL OUTTRAP('OFF')
IF RC>8 THEN DO
  SAY '**whurrr-fizzle-snap-fart**'
  SAY 'THE JOB TO CREATE THE UNIVERSE VSAM DATASET'
  SAY 'FAILED WITH A RETURN CODE OF 'RC'.'
  SAY 'You might want to check your outlist to get a handle'
  SAY 'on what caused your first day as a "god" to go so far'
  SAY 'off track.  The big bang sequence failed spectacularly.'
  SAY 'and your game cannot be initialized or restarted.'
  EXIT
  end
"CLEARS"
say "Beginning the BIGBANG sequence.  This may take a while."
say "Traditionally this sort of thing takes 6 days, and we rest up"
say "a bit on the seventh.  Technology has advanced a bit in the "
say "past couple of aeons, so it might not take that long even on"
say "the elderly hardware I've detected you're running."
say "So, sit back and relax.  Enjoy an adult beverage of your choice."
SAY ''
SAY 'PRESS ENTER TO BEGIN'
PULL ENT
STTIME=TIME('ELAPSED')

"CLEARS"
SAY 'Start time:'Time()
BLACKHOLES=0
LOTTERIES=0
SECTID=0
STL='CREATING SECTORS:         BH:      LOT:    (   %)'
CALL OPENDSN
MYSECTOR=0
DO TR=1 TO MAXROWS
   PTR=0
   DO TC=1 TO MAXCOLS
      PLAKEY=''
      PORKEY=''
      KPLANET=''
      KPORT=''
      THISREC=''
      MYSECTOR=MYSECTOR+1
      SECTID=MYSECTOR
      ROW=TR
      COL=TC
      SYSNAME='Unnamed System'
      OWNER=''
      BLACKHOLE=''
      LOTTERY=''
      SECKEY=FIXKEY('SEC'||STRIP(SECTID))
      CALL PUTPLANET
      CALL PUTPORT
      BH=RANDOM(0,5000,MILLIS())
      LT=RANDOM(0,5000,MILLIS())
      IF BH>4990 THEN DO
         BLACKHOLE='1'
         BLACKHOLES=BLACKHOLES+1
         END
      IF LT=4192 THEN DO
         LOTTERY='1'
         LOTTERIES=LOTTERIES+1
         END
      THISREC=SETRECORD('SECTOR')
      CALL PUTSECTOR
   END
   STL='CREATING SECTORS:         BH:      LOT:    (   %)'
   STL=OVERLAY(SECTID,STL,19)
   STL=OVERLAY(BLACKHOLES,STL,31)
   STL=OVERLAY(LOTTERIES,STL,41)
   RE=STATUS(STL)
END
CALL CLOSEDSN
ENDTIME=TIME('ELAPSED')
SAY 'END TIME: 'TIME()' Elapsed: 'TIME('ELAPSED')
EXIT

PUTPORT:
/* DECIDES IF A PORT WILL BE IN THIS SECTOR */
/* PORTS NEED TO BE PRETTY FREQUENT */
PORKEY=''
PP=0
PP=RANDOM(1,80,MILLIS())
IF PP>60 THEN
   DO
      PORTREC=''
      CALL SPLITRECORD 'PORT',''
      PORTS_PLACED=PORTS_PLACED+1
      TPORT=SECTID
      TROW=TR
      TCOL=RC
      TNAME='Port '||PORTS_PLACED
      TOWNER=0
      TFOOD=RANDOM(0,500000,MILLIS())
      TFPRICE=RANDOM(0,5000,MILLIS())
      TEQUIP=RANDOM(0,500000,MILLIS())
      TEPRICE=RANDOM(0,5000,MILLIS())
      TMATL=RANDOM(0,500000,MILLIS())
      TMPRICE=RANDOM(0,5000,MILLIS())
      TBANK=0
      PORKEY=FIXKEY('POR'||TPORT)
      PORTREC=SETRECORD('PORT')
      ADDRESS TSO
      "VSAMIO READ UNISET (KEY "PORKEY" UPDATE VAR CURRENT"
      "VSAMIO WRITE UNISET (KEY "PORKEY" VAR PORTREC"
      KPORT=PORKEY
      CALL CHECKLINES
      SAY 'Port: 'TNAME' placed in sector 'TPORT
      OLINES=OLINES+1
      IF RC<>0 THEN
         DO
           SAY "CANNOT WRITE PORT RECORD RC="RC", RCX="RCX
           EXIT
         END
   END
RETURN TPORT

PUTPLANET:
/* DECIDES ON A PLANET AND IF NECESSARY */
/* MODIFIES THE RECORD BEFORE WRITING   */
PLAKEY=''
PP=0
IF PTR>17 THEN RETURN 0
PP=RANDOM(1,160,MILLIS())
IF PP>120 THEN
   DO
     PLANREC=''
     CALL SPLITRECORD 'PLANET',''
     PFIGHTERS=0
     PPOSTURE=0
     PFOOD=0
     PMATL=0
     PEQUIP=0
     PMONEY=0
     PLANETS_PLACED=PLANETS_PLACED+1
     PLANET=PLANETS_PLACED
     PNAME='Planet '||PLANETS_PLACED
     PSECT=SECTID
     PROW=TR
     PCOL=TC
     POWNER=0
     PGENDEV=0
     PFIGHTERS=RANDOM(0,25000,MILLIS())
     PMINES=0
     PPOSTURE=0
     PFOOD=RANDOM(0,1000000,MILLIS())
     PEQUIP=RANDOM(0,1000000,MILLIS())
     PMATL=RANDOM(0,1000000,MILLIS())
     PMONEY=RANDOM(0,500000,MILLIS())
     PLAKEY=FIXKEY("PLA"||PSECT)
     KPLANET=PLAKEY
     PLANREC=SETRECORD('PLANET')
     ADDRESS TSO
     "VSAMIO READ UNISET (KEY "PLAKEY" UPDATE VAR CURRENT"
     "VSAMIO WRITE UNISET (KEY "PLAKEY" VAR PLANREC"
     IF RC<>0 THEN DO
        SAY "ERROR WRITING PLANET "RC" RCX "RCX
        EXIT
        END
     CALL CHECKLINES
     SAY "Planet "PNAME" placed in "PSECT" with $"PMONEY
     OLINES=OLINES+1
     PTR=PTR+1
     RETURN PLANETS_PLACED
   END
RETURN

CHECKLINES:
  IF OLINES>18 THEN
     DO
     "CLEARS"
     CALL STATUS(STL)
     END
RETURN

STATUS:
  ARG MSG
  ADDRESS TSO
  "CLEARS"
  PCT = FORMAT((TR/MAXROWS)*100)
  MSG=OVERLAY(PCT,MSG,45)
  SAY MSG
  OLINES=2
  RETURN PCT

FIXKEY:
  ARG INKEY
  INKEY=SUBSTR(INKEY,1,20,'_')
  OUTKEY=TRANSLATE(INKEY,'_',' ')
  RETURN OUTKEY

OPENDSN:
  ADDRESS TSO
  "ALLOC FILE(UNISET) DSN("UNIDSN") SHR"
  "VSAMIO OPEN UNISET (UPDATE"
  RETURN

CLOSEDSN:
  ADDRESS TSO
  "VSAMIO CLOSE UNISET"
  "FREE FILE(UNISET)"
  RETURN

PUTSECTOR:
  "VSAMIO READ UNISET (KEY "SECKEY" UPDATE VAR CURRENT"
  "VSAMIO WRITE UNISET (KEY "SECKEY" VAR THISREC"
  RETURN

MILLIS:
 RETURN RIGHT(TIME('LONG'),6)
